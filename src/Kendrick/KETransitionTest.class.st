Class {
	#name : #KETransitionTest,
	#superclass : #TestCase,
	#category : #'Kendrick-Tests-Core'
}

{ #category : #tests }
KETransitionTest >> testAsString [
	| aTransition |
	aTransition := KETransition from: {#status -> #S} to: {#status -> #I} probability: [ :f :t :m | (m atParameter: #beta) * f * t ].
	self assert: aTransition asString equals: 'From: {#status->#S} To: {#status->#I}'
]

{ #category : #tests }
KETransitionTest >> testAsStringWithEmptyCompartment [
	| aTransition |
	aTransition := KETransition from: {(#status -> #empty)} to: {#status -> #I} probability: [ :f :t :m | (m atParameter: #beta) * f * t ].
	self assert: aTransition asString equals: 'From: {#status->#empty} To: {#status->#I}'
]

{ #category : #tests }
KETransitionTest >> testEvaluateProbability [
	| tr model rs |
	model := KEModel new.
	model attributes: {(#status -> #(S I R))}.
	model atCompartment: {(#status -> #S)} put: 10000 atOthersPut: 0.
	model atCompartment: {(#status -> #I)} put: 1.
	tr := KETransition from: {#status -> #S} to: {#status -> #I} probability: [ :fr :to :t | (model atParameter: #beta) * fr * to ].
	model
		addParameters:
			{(#beta -> 0.0052).
			(#gamma -> 52)}.
	rs := tr probability value: (model atCompartment: tr from) value: (model atCompartment: tr to) value: 0.
	self assert: rs equals: 52
]

{ #category : #tests }
KETransitionTest >> testEvaluateProbabilityOfBirthTransition [
	| tr model prob rate |
	model := KEModel new.
	model attributes: {(#status -> #(S I R))}.
	model atCompartment: {(#status -> #S)} put: 99999 atOthersPut: 0.
	model atCompartment: {(#status -> #I)} put: 1.
	model addParameter:  (#mu -> 0.001).
	tr := KETransition from: {#status -> #empty} to: {#status -> #S} probability: [ :m | (m atParameter: #mu) ].
	
	prob := tr probability value: model.
	model currentCompartment: tr to.
	rate := prob * (model atCompartment: tr from).
	self assert: prob equals: 0.001.
	self assert: rate equals: 100.
]

{ #category : #tests }
KETransitionTest >> testEvaluateProbabilityOfBirthTransition2 [
	| tr model prob rate |
	model := KEModel new.
	model attributes: { #status->#(S I R). #region->#(A B C) }.
	model atCompartment: { #status->#S. #region->#A } put: 99999 atOthersPut: 0.
	model atCompartment: { #status->#I. #region->#A } put: 1.
	model addParameter:  (#mu -> 0.001).
	
	tr := KETransition from: {#status -> #empty. #region->#A} to: {#status -> #S. #region->#A} probability: [ :m | (m atParameter: #mu) ].
	
	prob := tr probability value: model.
	model currentCompartment: tr to.
	rate := prob * (model atCompartment: tr from).
	self assert: prob equals: 0.001.
	self assert: rate equals: 100.
]

{ #category : #tests }
KETransitionTest >> testExecuteTransition [
	| tr model |
	tr := KETransition from: {#status -> #S} to: {#status -> #I} probability: [ :f :t :m | (m atParameter: #beta) * f * t ].
	model := KEModel new.
	model attributes: {(#status -> #(S I R))}.
	model atCompartment: {(#status -> #S)} put: 10000 atOthersPut: 0.
	model atCompartment: {(#status -> #I)} put: 1.
	model
		addParameters:
			{(#beta -> 0.0052).
			(#gamma -> 52)}.
	tr executeOn: model times: 1.
	self assert: (model atCompartment: {(#status -> #S)}) equals: 9999.
	self assert: (model atCompartment: {(#status -> #I)}) equals: 2
]

{ #category : #tests }
KETransitionTest >> testFromAsString [
	| aTransition from to|
	from := '{#status: #S}'.
	to := '{#status: #I}'.
	aTransition := KETransition from: from to: to probability: [ :f :t :m | (m atParameter: #beta) * f * t ].
	self assert: aTransition asString equals: 'From: {#status->#S} To: {#status->#I}'
]
